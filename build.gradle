plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0" apply false
}

ext.set('output', "../output/")
project.buildDir = "../output/"

allprojects {
    group = "xyz.winston"
    version = "1.0.0-SNAPSHOT"
}

configure(subprojects) {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "com.github.johnrengelman.shadow"

    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            url = "https://oss.sonatype.org/content/repositories/snapshots"
        }

        maven { url 'https://jitpack.io' }

    }

    tasks {
        shadowJar {
            exclude 'META-INF/DEPENDENCIES'
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/NOTICE.txt'
            exclude 'META-INF/maven/**'
        }

        compileJava {
            targetCompatibility = JavaVersion.VERSION_11
            sourceCompatibility = JavaVersion.VERSION_11
            options.encoding = "UTF-8"
        }

        compileTestJava {
            targetCompatibility = JavaVersion.VERSION_11
            sourceCompatibility = JavaVersion.VERSION_11
            options.encoding = "UTF-8"
        }

        build {
            dependsOn(shadowJar)
        }

        jar {
            finalizedBy(publishToMavenLocal)
        }

        test {
            useJUnitPlatform()
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    dependencies {

        /** Logger */
        implementation 'org.apache.logging.log4j:log4j-api:2.14.1'
        implementation 'org.apache.logging.log4j:log4j-core:2.14.1'

        /** Ломбок */
        compileOnly "org.projectlombok:lombok:1.18.20"
        annotationProcessor "org.projectlombok:lombok:1.18.20"
        testCompileOnly "org.projectlombok:lombok:1.18.20"
        testAnnotationProcessor "org.projectlombok:lombok:1.18.20"

        /** Аннотации от JetBrains */
        compileOnly "org.jetbrains:annotations:20.1.0"

        /** Невероятная библиотека */
        compileOnly "xyz.winston:protocol:1.0.0-SNAPSHOT"

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.0'
    }
}